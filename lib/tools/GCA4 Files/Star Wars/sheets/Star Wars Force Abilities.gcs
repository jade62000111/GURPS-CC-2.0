'VBScript
'The line above must be the very first line of the file.
'
'NOTE:
'This GCS file will only work with characters saved by GCA 4!
'
'Last Updated: December 15, 2018
'
Const Stats = 1
Const Ads = 2
Const Disads = 3
Const Quirks = 4
Const Powers = 5
Const Perks = 5
Const Skills = 6
Const ForceAbilities = 7
Const Equipment = 8
Const Packages = 9	'Templates, Meta-traits

Const AlignLeftTop = 0
Const AlignCenterTop = 1
Const AlignRightTop = 2
Const AlignLeftBottom = 3
Const AlignCenterBottom = 4
Const AlignRightBottom = 5
Const AlignLeftMiddle = 6
Const AlignCenterMiddle = 7
Const AlignRightMiddle = 8
Const AlignJustifyTop = 9
Const AlignJustifyBottom = 10
Const AlignJustifyMiddle = 11

Const BorderNone = 0
Const BorderBottom = 1
Const BorderTop = 2
Const BorderTopBottom = 3
Const BorderBox = 4
Const BorderColumns = 5
Const BorderColumnTopBottom = 6
Const BorderAll = 7
Const BorderBoxRows = 8
Const BorderBoxColumns = 9
Const BorderBelowHeader = 10

Const Grey = &HCCCCCC

const optYesNo = 0
const optTrueFalse = 1 
const optList = 2	'not yet supported
const optListNumber = 3	'not yet supported
const optListFlag = 4	'not yet supported
const optColor = 5
const optFont = 6
const optText = 7

Dim FormFont, FormFontSize, FormFontBold, FormFontItalic
Dim UserFont, UserFontSize, UserFontBold, UserFontItalic
Dim FormFontColor, UserFontColor


'****************************************
'Creating options
'****************************************
Sub CharacterSheetOptions()

'AddOption(OptionName As Variant, 
'          Optional OptionType As Variant = 0, 
'          Optional OptionList As Variant = "", 
'          Optional OptionDefault As Variant = True, 
'          Optional UserPrompt As Variant = "Select an option")

'The spacer/headers used here are all headers. If you just want
'a spacer, then set UserPrompt = "" and no line will be drawn, either
OptionName = "HeaderFonts"
OptionType = -1
UserPrompt = "Font and Text Options"
Options.AddOption OptionName, OptionType, , , UserPrompt

OptionName = "FormFont"
OptionType = optFont
OptionDefault = "Arial|10"
UserPrompt = "Font for the form text items"
Options.AddOption OptionName, OptionType, , OptionDefault, UserPrompt

OptionName = "FormFontColor"
OptionType = optColor
OptionDefault = 0
UserPrompt = "Color for the form text items"
Options.AddOption OptionName, OptionType, , OptionDefault, UserPrompt

OptionName = "UserFont"
OptionType = optFont
OptionDefault = "Arial|10"
UserPrompt = "Font for the user text items"
Options.AddOption OptionName, OptionType, , OptionDefault, UserPrompt

OptionName = "UserFontColor"
OptionType = optColor
OptionDefault = 0
UserPrompt = "Color for the user text items"
Options.AddOption OptionName, OptionType, , OptionDefault, UserPrompt


OptionName = "HeaderOtherColor"
OptionType = -1
UserPrompt = "Other Color Options"
Options.AddOption OptionName, OptionType, , , UserPrompt

OptionName = "HeaderRowColor"
OptionType = optColor
OptionDefault = &HCCCCCC
UserPrompt = "Background color for the header row of the Force Abilities. (For this option, a color of pure Black (&&H000000) is the same as saying 'no color' or pure white.)"
Options.AddOption OptionName, OptionType, , OptionDefault, UserPrompt

OptionName = "AltColor"
OptionType = optColor
OptionDefault = 0
UserPrompt = "Background color for each alternate row of the Force Abilities. (For this option, a color of pure Black (&&H000000) is the same as saying 'no color' or pure white.)"
Options.AddOption OptionName, OptionType, , OptionDefault, UserPrompt


OptionName = "HeaderMisc"
OptionType = -1
UserPrompt = "Miscellaneous Options"
Options.AddOption OptionName, OptionType, , , UserPrompt

OptionName = "DoTitle"
OptionType = optYesNo
UserPrompt = "Print the 'Force Abilities of ' line at the top of the first page"
Options.AddOption OptionName, OptionType, , , UserPrompt

OptionName = "BoxForceAbilities"
OptionType = optYesNo
UserPrompt = "Print the borders around the rows (box each spell)?"
Options.AddOption OptionName, OptionType, , , UserPrompt

OptionName = "RitualMagic"
OptionType = optYesNo
UserPrompt = "Include a column for Prereq Count (for Ritual Magic)?"
Options.AddOption OptionName, OptionType, , False, UserPrompt


OptionName = "HeaderFooter"
OptionType = -1
UserPrompt = "Header and Footer Options"
Options.AddOption OptionName, OptionType, , , UserPrompt

OptionName = "DoHeader"
OptionType = optYesNo
UserPrompt = "Print a header?"
Options.AddOption OptionName, OptionType, , , UserPrompt

OptionName = "DoFooter"
OptionType = optYesNo
UserPrompt = "Print a footer?"
Options.AddOption OptionName, OptionType, , , UserPrompt

OptionName = "Footer"
OptionType = optText
OptionDefault = "Star Wars GCA 4|%d|http://members.upc.nl/l.deckers3/"
UserPrompt = "If you are printing a footer, the text for the footer of your character sheet"
Options.AddOption OptionName, OptionType, , OptionDefault, UserPrompt

End Sub

'****************************************
'The Main Module, where the form starts
'****************************************
Sub Main()

scalemode = 0   'inches

'get the font options
optionFormFont = Options.Value("FormFont")
FormFont = ReturnFontName(optionFormFont)
FormFontSize = ReturnFontSize(optionFormFont)
FormFontBold = ReturnFontBold(optionFormFont)
FormFontItalic = ReturnFontItalic(optionFormFont)
FormFontColor = Options.Value("FormFontColor")

optionUserFont = Options.Value("UserFont")
UserFont = ReturnFontName(optionUserFont)
UserFontSize = ReturnFontSize(optionUserFont)
UserFontBold = ReturnFontBold(optionUserFont)
UserFontItalic = ReturnFontItalic(optionUserFont)
UserFontColor = Options.Value("UserFontColor")

SetFormFont

Call SetMargins
Call SetHeader
Call SetFooter

CurrentY = MarginTop

if Options.Value("DoTitle") then
	FontSize = FormFontSize * 2
	PrintCentered "Force Abilities of " & Char.Name, MarginLeft, PageWidth - MarginRight
	FontSize = FormFontSize
	CurrentY = MarginTop + .5
end if

ColumnTop = CurrentY

NameCol = "<1.5"
if PageWidth < 8.5 then NameCol = "<1.25"

SetUserFont

StartTable
	if Options.Value("BoxForceAbilities") then
		TableBorder = BorderBoxRows
	else
		TableBorder = BorderBox 'BelowHeader
	end if
	if Options.Value("RitualForce") then
		Format = NameCol & "|<1|<0.5|<0.75|<0.75|<1|<0.625|<0.625|<0.875"
		TableHeader = "Force Ability|Class|Skill Level|Time to Cast|Duration|Cost to Cast/Maintain|Aspect, Facet, Focus|PreReq Count|Page No."
	else
		Format = NameCol & "|<1|<0.5|<0.75|<0.75|<1.125|<1.25|<0.875"
		TableHeader = "Force Ability|Class|Skill Level|Time to Cast|Duration|Cost to Cast/Maintain|Aspect, Facet, Focus|Page No."
	end if
	HdrColor = Options.Value("HeaderRowColor") 'Grey
	BackColor = 0
	Append = True
	
	for curItem = 1 to char.items.count
		If char.items(curitem).itemtype = ForceAbilities Then
			RowText = char.items(curitem).fullnametl
			RowText = RowText & "|" & char.items(curitem).tagitem("class")
			RowText = RowText & "|" & char.items(curitem).level
			RowText = RowText & "|" & char.items(curitem).tagitem("time")
			RowText = RowText & "|" & char.items(curitem).tagitem("duration")
			RowText = RowText & "|" & char.items(curitem).tagitem("castingcost")
			if Options.Value("RitualMagic") then
				RowText = RowText & "|" & char.items(curitem).tagitem("shortcat")
				RowText = RowText & "|" & char.items(curitem).tagitem("prereqcount")
			else
				RowText = RowText & "|" & char.items(curitem).tagitem("cat")			
			end if
			RowText = RowText & "|" & char.items(curitem).tagitem("page")

			AddTable Format, TableHeader, RowText, HdrColor, BackColor, Append
		End If
	next
	
	'Set the Header Row to the form font
	NumCols = TableColumns
	TableCellFontName(0,1,0,NumCols) = FormFont
	TableCellFontSize(0,1,0,NumCols) = FormFontSize
	TableCellFontBold(0,1,0,NumCols) = FormFontBold
	TableCellFontItalic(0,1,0,NumCols) = FormFontItalic
	if FormFontColor <> 0 then TableCellForeColor(0,1,0,NumCols) = FormFontColor else TableCellForeColor(0,1,0,NumCols) = 1

	NumRows = TableRows
	for i = 1 to NumRows
		if i mod 2 = 0 then	TableCellBackColor(i, 1, i, NumCols) = Options.Value("AltColor")
	next
EndTable


End Sub

'****************************************
'set the margins for the character sheets
'****************************************
Sub SetMargins()

MarginLeft = 0.5
MarginRight = 0.5
MarginTop = 0.5
MarginBottom = 0.5

End Sub

'****************************************
'Create the header for the character sheets
'****************************************
Sub SetHeader()

if Options.Value("DoHeader") then
	header = "Force Abilities of " & Char.Name
	HeaderFont = FormFont
	HeaderFontSize = 8
	HeaderColor = FormFontColor
end if

End Sub

'****************************************
'Create the footer for the character sheets
'****************************************
Sub SetFooter()

if Options.Value("DoFooter") then
	footer = Options.Value("Footer")
end if

End Sub

'****************************************
'Set Font to the User Prefs
'****************************************
Sub SetUserFont()

FontName = UserFont
FontSize = UserFontSize
FontBold = UserFontBold
FontItalic = UserFontItalic
TextColor = UserFontColor

End Sub

'****************************************
'Set Font to the Form Prefs
'****************************************
Sub SetFormFont()

FontName = FormFont
FontSize = FormFontSize
FontBold = FormFontBold
FontItalic = FormFontItalic
TextColor = FormFontColor

End Sub
